apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'net.corda.plugins.publish-utils'

description 'Corda node database schemas'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

sourceSets {
    main {
        kotlin {
            srcDir "${projectDir}/generated/source/kapt/main/"
        }
    }
}

dependencies {
    compile project(':core')

    // Requery: SQL based query & persistence for Kotlin
    kapt "io.requery:requery-processor:$requery_version"

    // For H2 database support in persistence
    testCompile "com.h2database:h2:1.4.193"
}

// Generated files will be removed by gradle clean. Since we aren't running kapt every time we must preserve these files
// Note: The lines that actually invoke kapt are those in the project.afterEvaluate block.
task buildKapt(type: Copy) {
    from "${buildDir}/generated"
    into "${projectDir}/generated"
}

// Remove the dependency on compile in kapt to avoid automatic kapt running - run :node-schemas:kaptKotlin explicitly.
project.afterEvaluate {
    compileKotlin.dependsOn.remove kaptKotlin
    buildKapt.dependsOn kaptKotlin
    buildKapt.mustRunAfter kaptKotlin
}